@Egg_Hatch
LockAllObjs
Message EggHatchText
AwaitMessageComplete
CloseMessage
HatchEgg
AwaitReturnToField
UnlockAllObjs
End

@Daycare_Start
LockAllObjs
LookTowardsObj Var.LastTalked 0xFFFF
GetDaycareState
GoToIf Daycare_OneMon Var.SpecialVar_Result C.Equal DaycareState.OnePokemon
GoToIf Daycare_TwoMon Var.SpecialVar_Result C.Equal DaycareState.TwoPokemon
GoToIf Daycare_EggWaiting Var.SpecialVar_Result C.Equal DaycareState.EggWaiting
Message DaycareText_CanITake
AwaitMessageRead
YesNoChoice
GoToIf Daycare_TryTake Var.SpecialVar_Result C.Equal 1
CloseMessage
UnlockAllObjs
End

#Daycare_OneMon
Message DaycareText_GoodToSeeYou1
AwaitMessageComplete
Message DaycareText_CanITake
AwaitMessageRead
YesNoChoice
GoToIf Daycare_TryTake Var.SpecialVar_Result C.Equal 1
CloseMessage
UnlockAllObjs
End

#Daycare_TwoMon
Message DaycareText_GoodToSeeYou2
AwaitMessageComplete
GetDaycareCompatibility
CallIf Daycare_CompatMax Var.SpecialVar_Result C.Equal 70
CallIf Daycare_CompatMed Var.SpecialVar_Result C.Equal 50
CallIf Daycare_CompatMin Var.SpecialVar_Result C.Equal 20
CallIf Daycare_CompatNone Var.SpecialVar_Result C.Equal 0
AwaitMessageComplete
CloseMessage
UnlockAllObjs
End

#Daycare_EggWaiting
Message DaycareText_MonHadEggOMG
AwaitMessageRead
YesNoChoice
GoToIf Daycare_TryTakeEgg Var.SpecialVar_Result C.Equal 1
DisposeDaycareEgg
CloseMessage
UnlockAllObjs
End

#Daycare_TryTakeEgg
CountPlayerParty
GoToIf Daycare_CantTakeEgg Var.SpecialVar_Result C.Equal 6
GiveDaycareEgg
CloseMessage
UnlockAllObjs
End

#Daycare_TryTake
CountNonFaintedNonEggParty
GoToIf Daycare_CantTake Var.SpecialVar_Result C.Equal 1
Message DaycareText_Test_Take
AwaitMessageComplete
CloseMessage
SelectDaycareMon
CallIf Daycare_Store Var.SpecialVar_Result C.GreaterEqual 0
UnlockAllObjs
End

#Daycare_CantTake
Message DaycareText_CantTake
AwaitMessageComplete
CloseMessage
UnlockAllObjs
End

#Daycare_CantTakeEgg
Message DaycareText_CantTakeEgg
AwaitMessageComplete
CloseMessage
UnlockAllObjs
End

#Daycare_Store
StorePokemonInDaycare
Message DaycareText_IWillRaise
AwaitMessageComplete
CloseMessage
return

#Daycare_CompatMax
Message DaycareTest_CompatMax
return
#Daycare_CompatMed
Message DaycareTest_CompatMed
return
#Daycare_CompatMin
Message DaycareTest_CompatMin
return
#Daycare_CompatNone
Message DaycareTest_CompatNone
return

// Text

#EggHatchText
"Huh?"

#DaycareText_GoodToSeeYou1
"Good to see you, your Pokémon\n
is doing well."

#DaycareText_GoodToSeeYou2
"Good to see you, your Pokémon\n
are doing well."

#DaycareText_MonHadEggOMG
"Your Pokémon had an Egg!\n
Would you like it?"

#DaycareText_CanITake
"Can I take your Pokémon?"

#DaycareText_Test_Take
"Which Pokémon should I take?"

#DaycareText_CantTake
"That's your last Pokémon, I can't\n
take it."

#DaycareText_CantTakeEgg
"Your party has no room for this Egg,\n
come back once you've made room."

#DaycareText_IWillRaise
"I'll raise your {BUF 0}."

#DaycareTest_CompatMax
"The two seem to get along very well!"
#DaycareTest_CompatMed
"The two seem to get along."
#DaycareTest_CompatMin
"The two don't really seem to like each other very much."
#DaycareTest_CompatNone
"The two prefer to play with other Pokémon\n
more than with each other."